plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
}

def credentialsPropertiesFile = rootProject.file("credentials.properties")
def credentialsProperties = new Properties()
credentialsProperties.load(new FileInputStream(credentialsPropertiesFile))

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.santukis.cleanarchitecture"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }

        buildConfigField "String", "RIJKSMUSEUM_API_KEY", credentialsProperties['rijksMuseumApiKey']
        buildConfigField "String", "RIJKSMUSEUM_END_POINT", '"https://www.rijksmuseum.nl/"'
        buildConfigField "String", "MET_END_POINT", '"https://collectionapi.metmuseum.org/"'
        buildConfigField "String", "CHICAGO_END_POINT", '"https://api.artic.edu/"'
        buildConfigField "String", "CLEVELAND_END_POINT", '"https://openaccess-api.clevelandart.org/"'
        buildConfigField "String", "HARDVARD_API_KEY", credentialsProperties['harvardApiKey']
        buildConfigField "String", "HARDVARD_END_POINT", '"https://api.harvardartmuseums.org/"'
        buildConfigField "String", "WALTERS_API_KEY", credentialsProperties['waltersApiKey']
        buildConfigField "String", "WALTERS_END_POINT", '"http://api.thewalters.org/"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'


        }
        debug {

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }

    sourceSets {

        androidTest {
            java.srcDirs += "src/sharedTest/java"
        }

        test {
            java.srcDirs += "src/sharedTest/java"
        }

        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

    testOptions.unitTests{
        includeAndroidResources = true
    }
}

dependencies {

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    //Kodein
    implementation 'org.kodein.di:kodein-di-framework-android-core:7.15.0'
    implementation 'org.kodein.di:kodein-di-framework-android-x:7.15.0'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation('com.squareup.retrofit2:converter-moshi:2.9.0')
    implementation("com.squareup.moshi:moshi:1.14.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.14.0")
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'

    //Room
    def room_version = '2.4.3'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    //Navigator
    def nav_version = '2.5.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.github.santukis:navigator:1.1.1@aar'

    //LiveData
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    //ThreeTen
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.0'

    //Proteo
    implementation 'com.github.santukis:proteo:1.2.6@aar'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    //Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation("com.squareup.okhttp3:mockwebserver:4.9.3")
    testImplementation("com.squareup.okhttp3:okhttp:4.9.3")
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "androidx.test.ext:junit-ktx:1.1.5"
    testImplementation "androidx.test:core-ktx:1.5.0"
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    testImplementation 'org.mockito:mockito-core:3.9.0'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

}